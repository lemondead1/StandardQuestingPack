plugins {
    id "maven-publish"
    id "idea"
    id "net.minecraftforge.gradle" version "5.+"
}

minecraft {
    mappings channel: "stable", version: "39-1.12"

    runs {
        server {
            workingDirectory project.file("run")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
        }

        client {
            workingDirectory project.file("run")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
        }
    }
}

group "bq_standard"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs "libs"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraftVersion}-${forgeVersion}"

    implementation ":BetterQuesting:3.6.0:api"
    implementation ":jei_1.12.2:4.16.1.1003"
}

processResources {
    filesMatching("mcmod.info") {
        expand "version": project.version, "mcversion": project.minecraftVersion
    }
}

jar {
    finalizedBy "reobfJar"
}

sourceSets.main {
    output.resourcesDir = output.classesDirs.singleFile
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    archiveClassifier = "deobf"
}

artifacts {
    archives deobfJar
}
